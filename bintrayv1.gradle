apply plugin: 'com.jfrog.bintray'

version = libraryVersion

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        excludes = ['**/*.kt'] // Exclude all kotlin files from javadoc file.
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = bintrayUser
    key = bintrayPassword
    override = true

    configurations = ['archives']
    pkg {
        userOrg = 'dialog'
        repo = 'android-sdk'
        name = artifact
        desc = libraryDescription
        websiteUrl = 'https://dlg.im'
        vcsUrl = gitUrl
        licenses = ["Dialog"]
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                if (hasProperty('bintrayGpgPassword')) {
                    bintray.pkg.version.gpg.passphrase = bintrayGpgPassword
                }
            }
        }
    }
}
